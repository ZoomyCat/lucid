# $Id: PKGBUILD 203563 2014-01-13 17:11:39Z andyrtr $
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>

pkgname=dirmngr
pkgver=1.1.1
pkgrel=3
pkgdesc="A daemon to handle CRL and certificate requests"
arch=('i686' 'x86_64')
license=('GPL')
url="ftp://ftp.gnupg.org/gcrypt/dirmngr"
depends=('libgcrypt' 'libldap' 'libksba' 'libgpg-error' 'libassuan' 'pth')
source=(ftp://ftp.gnupg.org/gcrypt/$pkgname/$pkgname-$pkgver.tar.bz2
		'dirmngr-pth-fix.patch')
install=dirmngr.install

prepare(){
	cd $srcdir/$pkgname-$pkgver
	patch -p0 -i $srcdir/dirmngr-pth-fix.patch
}

build() {
  cd $srcdir/$pkgname-$pkgver
  # FIX for https://bugzilla.redhat.com/565131
  # not sure how best to turn this into something more upstreamable
  # patch configure to try this combo first? -- Rex
  # https://bugs.archlinux.org/task/28606
  export LDAPLIBS="-lldap -llber"
  ./configure --prefix=/usr  --libexecdir=/usr/lib
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=$pkgdir install
}
sha256sums=('d2280b8c314db80cdaf101211a47826734443436f5c3545cc1b614c50eaae6ff'
            '5c087b1bef1d40abeee5361272af3c32eeb0dbd7127ff6a96dd4ead0a4af9ef7')
