# Maintainer: artoo <flower_of_life@gmx.net>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinxu.org>
# Contributor: Wael Nasreddine <gandalf@siemens-mobiles.org>
# Contributor: Tor Krill <tor@krill.nu>
# Contributor: Will Rea <sillywilly@gmail.com>
# Contributor: Valentine Sinitsyn <e_val@inbox.ru>

_name=NetworkManager
_gentoo_uri="http://sources.gentoo.org/cgi-bin/viewvc.cgi/gentoo-x86/net-misc/networkmanager/files"
pkgbase=networkmanager-openrc
pkgname=(networkmanager-openrc libnm-glib-eudev)
pkgver=0.9.8.10
pkgrel=9
pkgdesc="Network Management daemon for consolekit and openrc"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.gnome.org/projects/${_name}/"
groups=('openrc' 'openrc-misc')
makedepends=('intltool' 'dhcpcd' 'dhclient' 'iptables' 'gobject-introspection'
			'gtk-doc' 'git' 'ppp' 'modemmanager' 'dbus-glib' 'iproute2' 'libnl'
			'nss' 'wpa_supplicant' 'dhcp-client' 'polkit-consolekit'
			'libsoup' 'libeudev')
source=("http://ftp.gnome.org/pub/gnome/sources/${_name}/${pkgver:0:3}/${_name}-${pkgver}.tar.xz"
        'disable_set_hostname.patch'
        "${_gentoo_uri}/10-openrc-status-r4"
        "${_gentoo_uri}/conf.d.${_name}"
        "${_gentoo_uri}/init.d.${_name}"
        "${_gentoo_uri}/nm-system-settings.conf-ifnet"
        "${_gentoo_uri}/networkmanager-0.9.8.4-pre-sleep.patch")
sha256sums=('064d27223d3824859df12e1fb25b787fec1c68bbc864dc52a0289b9211c4c972'
            '25056837ea92e559f09563ed817e3e0cd9333be861b8914e45f62ceaae2e0460'
            'f8ed424818b866a0bf882c569f4484e8b1485ce7ac8c472f060fd877f2dcfe65'
            '4594573f01fe5e04b6dde4525796acf909158591bdcefd662ec23fe0d1c3e1bd'
            '5f368362ef5c6deed538f20dc582d6da3c86871bd42297fa78536c0312021843'
            '34f53d9ac1ad65eda6942dc3059117f60f6321059ca23f4fbfcc2f2edcae151f'
            'ea70654524d5b414fcef10164b8dfb5e84bdf2444341f62fc7617952e2e721de')

prepare() {
  cd ${_name}-${pkgver}
  patch -Np1 -i ${srcdir}/disable_set_hostname.patch

  patch -Np1 -i ${srcdir}/networkmanager-0.9.8.4-pre-sleep.patch

}

build() {
  cd ${_name}-${pkgver}

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --libexecdir=/usr/lib/networkmanager \
    --sbindir=/usr/bin \
    --without-netconfig \
    --with-crypto=nss \
    --with-dhclient=/usr/bin/dhclient \
    --with-dhcpcd=/usr/bin/dhcpcd \
    --with-iptables=/usr/bin/iptables \
    --with-udev-dir=/usr/lib/udev \
    --with-resolvconf=/usr/bin/resolvconf \
    --with-session-tracking=ck \
    --with-modem-manager-1 \
    --disable-static \
    --enable-ifnet \
    --enable-more-warnings=no \
    --disable-wimax \
    --enable-modify-system \
    --enable-doc \
    --with-pppd-plugin-dir=/usr/lib/pppd/2.4.6

  make
}

package_networkmanager-openrc(){
	depends=('libnm-glib-eudev' 'iproute2' 'libnl' 'polkit-consolekit'
			'wpa_supplicant' 'dhcp-client' 'libsoup' 'dbus-openrc')
	optdepends=('dhclient: DHCPv6 support'
				'dnsmasq: Connection sharing'
				'bluez: Bluetooth support'
				'openresolv: resolvconf support'
				'ppp: Dialup connection support')
	provides=('networkmanager')
	conflicts=('networkmanager')
	groups=('openrc' 'openrc-misc')
	install=networkmanager.install
	backup=('etc/NetworkManager/NetworkManager.conf')

	cd ${_name}-${pkgver}
	make DESTDIR="${pkgdir}" install
	make DESTDIR="$pkgdir" install
	make DESTDIR="$pkgdir" -C libnm-glib uninstall
	make DESTDIR="$pkgdir" -C libnm-util uninstall
	make DESTDIR="$pkgdir" -C vapi uninstall

	# Some stuff to move is left over
	mv "$pkgdir/usr/include" ..
	mv "$pkgdir/usr/lib/pkgconfig" ..

	install -m644 "${srcdir}/nm-system-settings.conf-ifnet" "${pkgdir}/etc/${_name}/${_name}.conf"

	rm -r "${pkgdir}/var/run"

	install -Dm755 "${srcdir}/conf.d.${_name}" "${pkgdir}/etc/conf.d/networkmanager"
	install -Dm755 "${srcdir}/init.d.${_name}" "${pkgdir}/etc/init.d/networkmanager"
	install -Dm755 "${srcdir}/10-openrc-status-r4" "${pkgdir}/etc/${_name}/dispatcher.d/10-openrc-status"

	sed -e 's|@EPREFIX@/sbin|/bin|' -e 's|@EPREFIX@/usr/sbin|/usr/bin|' -e 's/NetworkManager/networkmanager/g' -e 's|#!/bin/sh|#!/usr/bin/sh|' -i "${pkgdir}/etc/${_name}/dispatcher.d/10-openrc-status"
	sed -e 's|#!/sbin/runscript|#!/usr/bin/runscript|' -e 's|/usr/sbin|/usr/bin|g' -i "${pkgdir}/etc/init.d/networkmanager"
}

package_libnm-glib-eudev() {
  pkgdesc="NetworkManager library"
  depends=('libeudev' 'nss' 'dbus-glib' 'libutil-linux')
  provides=('libnm-glib')
  conflicts=('libnm-glib')

  install -d "$pkgdir/usr/lib"
  mv include "$pkgdir/usr"
  mv pkgconfig "$pkgdir/usr/lib"

  cd ${_name}-$pkgver
  make DESTDIR="$pkgdir" -C libnm-util install
  make DESTDIR="$pkgdir" -C libnm-glib install
  make DESTDIR="$pkgdir" -C vapi install
}
