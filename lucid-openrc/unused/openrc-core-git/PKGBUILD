# Maintainer: artoo <flower_of_life@gmx.net>
# Contributor: williamh <williamh@gentoo.org>

_pkgname="openrc"

_gentoo_uri="http://sources.gentoo.org/cgi-bin/viewvc.cgi/gentoo-x86"

_udev="udev-init-scripts-git"
_net="netifrc-git"

pkgname=openrc-core-git
pkgdesc="Gentoo's universal init system, udev enabled."
pkgver=r2422.143f1c6
pkgrel=1
pkgdesc="Gentoo's universal init system, udev enabled."
arch=('i686' 'x86_64')
url="http://www.gentoo.org/proj/en/base/openrc/"
license=('GPL2')
groups=('openrc' 'openrc-base')
provides=('openrc-core')
makedepends=('inetutils' 'psmisc' 'udev>=186' 'git')
depends=('inetutils'
		'psmisc'
		'sysvinit'
		'udev>=186')
optdepends=('dhcpcd-openrc: dhcpcd initscript')
conflicts=('initscripts'
		'systemd-sysvcompat'
		'openrc'
		'openrc-git'
		'openrc-core')
install=${_pkgname}.install
backup=('etc/rc.conf'
		'etc/conf.d/consolefont'
		'etc/conf.d/keymaps'
		'etc/conf.d/hostname'
		'etc/conf.d/modules'
		'etc/conf.d/net'
		'etc/conf.d/hwclock')
source=("$pkgname"::'git://github.com/OpenRC/openrc.git'
		"${_udev}"::"git://git.overlays.gentoo.org/proj/udev-gentoo-scripts.git"
		"${_net}::git://git.overlays.gentoo.org/proj/netifrc.git"
		"${_pkgname}.logrotate"
		"${_gentoo_uri}/sys-apps/kmod/files/kmod-static-nodes")
sha256sums=('SKIP'
            'SKIP'
            'SKIP'
            '0b44210db9770588bd491cd6c0ac9412d99124c6be4c9d3f7d31ec8746072f5c'
            '078b07581e63b5113cef9e85da7d5b53e1fbad91d793f7fa8b7188b364689b0e')

_shebang='s|#!/sbin/runscript|#!/usr/bin/openrc-run|'
_binpath=('s|/sbin|/usr/bin|g')

_base_args=(SYSCONFDIR=/etc)
# Source the /etc/os-release file for branding
if [ -f /etc/os-release ]; then
	. /etc/os-release
	_base_args+=(BRANDING="$NAME")
else
	_base_args+=(BRANDING='Unknown Linux')
fi
_base_args+=(PREFIX=/usr)
_base_args+=(SBINDIR=/usr/bin)

_rc_args=( "${_base_args[@]}" )
_rc_args+=(LIBEXECDIR=/usr/lib/rc)
_rc_args+=(MKSELINUX=no)
_rc_args+=(MKPAM=pam)
_rc_args+=(MKTERMCAP=ncurses)
_rc_args+=(MKNET=no)

_net_args=( "${_base_args[@]}" )
_net_args+=(LIBEXECDIR=/usr/lib/netifrc)

pkgver() {
	cd "$srcdir/$pkgname"
	#git describe --long | sed -r 's/([^-]*-g)/r\1/;s/-/./g'
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare(){
	cd "${srcdir}/$pkgname"
	sed -e "${_binpath}" -i support/sysvinit/inittab
	sed -i 's:0444:0644:' mk/sys.mk
}

build(){
	# make netifrc
	cd "${srcdir}/${_net}"
	make "${_net_args[@]}"

	# make openrc
	cd "${srcdir}/$pkgname"
	make "${_rc_args[@]}"
}

_install_udev(){
	cd "${srcdir}/${_udev}"
	make DESTDIR="${pkgdir}" install
	# fix shebang & path to udevd
	#local _bin='s|/sbin/udevd|/usr/bin/udevd|g'
	sed -e "${_shebang}" -e "${_binpath}" -i "${pkgdir}/etc/init.d/udev"
	sed -e "${_shebang}" -i "${pkgdir}/etc/init.d/udev-mount"

	cd "${srcdir}/$pkgname"
	ln -sf "/etc/init.d/udev" "${pkgdir}/etc/runlevels/sysinit/udev"
	ln -sf "/etc/init.d/udev-mount" "${pkgdir}/etc/runlevels/sysinit/udev-mount"
}

_install_netifrc(){
	cd "${srcdir}/${_net}"
	make DESTDIR="${pkgdir}" "${_net_args[@]}" install
	install -Dm 644 "${srcdir}/${_net}/doc/net.example" "${pkgdir}/etc/conf.d/net"
	# create runlevel
	ln -sf "/etc/init.d/net.lo" "${pkgdir}/etc/runlevels/boot/net.lo"
}

_install_kmod(){
	cd "${srcdir}/$pkgname"
	install -Dm755 "${srcdir}/kmod-static-nodes" "${pkgdir}/etc/init.d/kmod-static-nodes"
	# fix shebang
	sed -e "${_shebang}" -i "${pkgdir}/etc/init.d/kmod-static-nodes"
	# create runlevel
	ln -sf "/etc/init.d/kmod-static-nodes" "${pkgdir}/etc/runlevels/sysinit/kmod-static-nodes"
}

package(){
	cd "${srcdir}/$pkgname"
	make DESTDIR="${pkgdir}" "${_rc_args[@]}" install
	# inittab
	install -m644 "${srcdir}/$pkgname/support/sysvinit/inittab" "${pkgdir}/etc/inittab"
	# logrotate
	install -Dm644 "${srcdir}/${_pkgname}.logrotate" "${pkgdir}/etc/logrotate.d/${_pkgname}"
	# enable unicode & logger
	sed -e 's/#unicode="NO"/unicode="YES"/' -e 's/#rc_logger="YES"/rc_logger="YES"/' -i "${pkgdir}/etc/rc.conf"
	# udev
	_install_udev
	# netifrc
	_install_netifrc
	# kmod-static-nodes
	_install_kmod
}

