# Based on the file created for Arch Linux by:
# Maintainer: Jesus Alvarez <jeezusjr at gmail dot com>
# Contributor: Kyle Fuller <inbox at kylefuller dot co dot uk>

# Maintainer: Lane Wiscombe[at]dem0n[dot]com

pkgname=('zfs-utils')
pkgver=0.6.3
pkgrel=1
pkgdesc="User-Mode utils for the Zettabyte File System."
depends=("spl-utils=$pkgver")
arch=('i686' 'x86_64')
url='http://zfsonlinux.org/'
source=(http://archive.zfsonlinux.org/downloads/zfsonlinux/zfs/zfs-$pkgver.tar.gz
        zfs-utils.bash-completion
        zfs-utils.initcpio.hook
        zfs-utils.initcpio.install
        zfs-utils.service)
groups=('lucidzfs')
sha256sums=('29bcfdd4161521692d2ca2515ad27a9edd9491c9cb5c6fb1e923d8eed28d391b'
            '15e742477fad0104871fc055b6ce9bf803540070e47fa515ea7ca3c1a401f831'
            '73662d13e6231c5034df6046926bcaf25fdba0aa53c9064693bcf2147f9c400a'
            '1ea6d2cdd27798680a96c9ebf18e9167b0575d032c7cfc731f16456cd38f2040'
            '6cc2264bcbf299d2f0651c05dc4cd8db5691ed2eb0623605aeeaa2dcba2c116b')
license=('CDDL')

build() {
  cd ${srcdir}/zfs-$pkgver
  ./autogen.sh
  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --sbindir=/usr/bin \
              --libdir=/usr/lib \
              --datadir=/usr/share \
              --includedir=/usr/include \
              --with-udevdir=/lib/udev \
              --libexecdir=/usr/lib/zfs-$pkgver \
              --with-config=user
  make
}

package() {
  cd ${srcdir}/zfs-$pkgver
  make DESTDIR=${pkgdir} install

  # Remove uneeded files
  rm -r ${pkgdir}/etc/init.d
  rm -r ${pkgdir}/usr/lib/dracut

  # move module tree /lib -> /usr/lib
  cp -r ${pkgdir}/{lib,usr}
  rm -r ${pkgdir}/lib

  # move module sbin -> /usr/bin
  cp -r ${pkgdir}/sbin/* ${pkgdir}/usr/bin
  rm -r ${pkgdir}/sbin

  install -D -m644 ${srcdir}/zfs-utils.initcpio.hook ${pkgdir}/usr/lib/initcpio/hooks/zfs
  install -D -m644 ${srcdir}/zfs-utils.initcpio.install ${pkgdir}/usr/lib/initcpio/install/zfs
  install -D -m644 ${srcdir}/zfs-utils.service ${pkgdir}/usr/lib/systemd/system/zfs.service
  install -D -m644 ${srcdir}/zfs-utils.bash-completion ${pkgdir}/usr/share/bash-completion/completions/zfs
}
