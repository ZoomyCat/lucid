# Based on the file created for Arch Linux by:
# Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Bob Fanger < bfanger(at)gmail >
# Filip <fila pruda com>, Det < nimetonmaili(at)gmail >

# Maintainer: Lane Wiscombe anex[at]dem0n[dot]com

pkgname=linux314-r8168
_pkgname=r8168
pkgver=8.037.00
pkgrel=4
pkgdesc="A kernel module for Realtek 8168 network cards"
url="http://www.realtek.com.tw"
license=("GPL")
arch=('i686' 'x86_64')
depends=('glibc' 'linux314')
makedepends=('linux314-headers')
provides=("$_pkgname=$pkgver")
groups=('linux314-extramodules')
source=(http://r8168.googlecode.com/files/$_pkgname-$pkgver.tar.bz2)
install=$_pkgname.install
sha256sums=('06aa1c6a58a878450798100eb5f9c3d87763c5f41e99b7d5f3b2fe78646d02b0')

_extramodules=extramodules-3.14-LUCID
_kernver="$(cat /usr/lib/modules/${_extraver}/version)"

build() {
	cd "$_pkgname-$pkgver"

	# avoid using the Makefile directly -- it doesn't understand
	# any kernel but the current.
	make -C /usr/lib/modules/$_kernver/build \
	SUBDIRS="$srcdir/$_pkgname-$pkgver/src" \
	EXTRA_CFLAGS="-DCONFIG_R8168_NAPI -DCONFIG_R8168_VLAN" \
	modules
}

package() {
	cd "$srcdir/$_pkgname-$pkgver/src"
	install -D -m644 $_pkgname.ko "$pkgdir/usr/lib/modules/$_extramodules/$_pkgname.ko"

	# set the kernel we've built for inside the install script
	sed -i -e "s/EXTRAMODULES=.*/EXTRAMODULES=${_extramodules}/g" "${startdir}/${_pkgname}.install"

	find "$pkgdir" -name '*.ko' -exec gzip -9 {} \;
}
