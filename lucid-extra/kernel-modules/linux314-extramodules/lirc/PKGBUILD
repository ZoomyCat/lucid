# Based on the file created for Arch Linux by:
# Paul Mattal <paul@archlinux.org>

# Maintainer: Guinux <nuxgui@gmail.com>
# Co-Maintainer: Lane Wiscombe anex[at]dem0n[dot]com

pkgbase=lirc
pkgname=('linux314-lirc' 'lirc-utils')
pkgver=0.9.0
pkgrel=4
epoch=1
_extramodules=extramodules-3.14-LUCID
arch=('i686' 'x86_64')
url="http://www.lirc.org/"
license=('GPL')
makedepends=('help2man' 'linux314' 'linux314-headers' 'alsa-lib' 'libx11' 'libftdi' 'libirman' 'python2')
options=('!makeflags' '!strip')
source=(http://prdownloads.sourceforge.net/${pkgbase}/${pkgbase}-${pkgver}.tar.bz2
        lirc_wpc8769l.patch
        lircd-handle-large-config.patch
        lirc_atiusb-kfifo.patch
        kernel-2.6.39.patch
	lircd lircmd lirc.logrotate lircd.conf irexec.conf irexecd
	kernel-3.8.0.patch)
md5sums=('b232aef26f23fe33ea8305d276637086'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP')

build() {
  _kernver="$(cat /usr/lib/modules/${_extramodules}/version)"
  cd "${srcdir}/lirc-${pkgver}"
  patch -Np1 -i "${srcdir}/lirc_wpc8769l.patch"
  patch -Np1 -i "${srcdir}/lircd-handle-large-config.patch"
  patch -Np1 -i "${srcdir}/lirc_atiusb-kfifo.patch"
  patch -Np1 -i "${srcdir}/kernel-2.6.39.patch"
  patch -Np1 -i "${srcdir}/kernel-3.8.0.patch"

  # Fix the AM_CONFIG_HEADER obsolete error
  sed -i 's/AM_CONFIG_HEADER(config.h)/AC_CONFIG_HEADERS(\[config.h\])/' configure.ac

  sed -i '/AC_PATH_XTRA/d' configure.ac
  sed -e 's/@X_CFLAGS@//g' \
      -e 's/@X_LIBS@//g' \
      -e 's/@X_PRE_LIBS@//g' \
      -e 's/@X_EXTRA_LIBS@//g' -i Makefile.am tools/Makefile.am
  libtoolize
  autoreconf

  PYTHON=python2 ./configure --enable-sandboxed --prefix=/usr \
      --with-driver=all --with-kerneldir=/lib/modules/${_kernver}/build/ \
      --with-moduledir=/usr/lib/modules/${_kernver}/kernel/drivers/misc \
      --with-transmitter

  # Remove drivers already in kernel
  sed -e "s:lirc_dev::" -e "s:lirc_bt829::" -e "s:lirc_igorplugusb::" \
      -e "s:lirc_imon::" -e "s:lirc_parallel::" -e "s:lirc_sasem::" \
      -e "s:lirc_serial::" -e "s:lirc_sir::" -e "s:lirc_ttusbir::" \
      -i Makefile drivers/Makefile drivers/*/Makefile tools/Makefile 
  make moduledir="/usr/lib/modules/${_extramodules}"
}

package_linux314-lirc() {
  pkgdesc="Linux Infrared Remote Control kernel modules for stock arch kernel"
  depends=('lirc-utils' 'linux314')
  replaces=('lirc+pctv')
  provides=("$_pkgname=$pkgver")
  groups=('linux314-extramodules')
  install=lirc.install

  cd "${srcdir}/lirc-${pkgver}/drivers"
  make DESTDIR="${pkgdir}" moduledir="/usr/lib/modules/${_extramodules}" install

  # set the kernel we've built for inside the install script
  sed -i -e "s/EXTRAMODULES=.*/EXTRAMODULES=${_extramodules}/g" "${startdir}/lirc.install"
  # gzip -9 modules
  find "${pkgdir}" -name '*.ko' -exec gzip -9 {} \;
}

package_lirc-utils() {
  pkgdesc="Linux Infrared Remote Control utils"
  depends=('alsa-lib' 'libx11' 'libftdi' 'libirman')
  optdepends=('python2: pronto2lirc utility')
  options=('strip' '!libtool')
  backup=('etc/conf.d/lircd.conf' 'etc/conf.d/irexec.conf')

  cd "${srcdir}/lirc-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -d "${pkgdir}/usr/share/lirc" "${pkgdir}/etc/rc.d"
  cp "${srcdir}"/{lircd,lircmd,irexecd} "${pkgdir}/etc/rc.d"
  cp -rp remotes "${pkgdir}/usr/share/lirc"
  chmod -R go-w "${pkgdir}/usr/share/lirc/"

  # install the logrotate config
  install -Dm644 "${srcdir}/lirc.logrotate" "${pkgdir}/etc/logrotate.d/lirc"
    
  # install conf.d file
  install -Dm644 "${srcdir}/lircd.conf" "${pkgdir}/etc/conf.d/lircd.conf"

  # install conf.d file
  install -Dm644 "${srcdir}/irexec.conf" "${pkgdir}/etc/conf.d/irexec.conf"

  install -d "${pkgdir}/etc/lirc"
  
  # remove built modules
  rm -r "${pkgdir}/usr/lib/"
}
